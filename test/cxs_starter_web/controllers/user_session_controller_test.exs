defmodule CxsStarterWeb.UserSessionControllerTest do
  use CxsStarterWeb.ConnCase, async: true

  import CxsStarter.AccountsFactory

  setup do
    now = NaiveDateTime.utc_now() |> NaiveDateTime.truncate(:second)

    user = user_factory(%{confirmed_at: now})

    %{user: user}
  end

  describe "GET /users/log_in" do
    @tag :unit
    test "renders log in page", %{conn: conn} do
      conn = get(conn, Routes.user_session_path(conn, :new))
      response = html_response(conn, 200)
      assert response =~ "Sign in"
      assert response =~ "Sign in</a>"
      assert response =~ "Register</a>"
    end

    @tag :unit
    test "redirects if already logged in", %{conn: conn, user: user} do
      conn = conn |> sign_in_user(user) |> get(Routes.user_session_path(conn, :new))
      assert redirected_to(conn) == "/"
    end
  end

  describe "POST /users/log_in" do
    @tag :unit
    test "logs the user in", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{
            "name" => user.name,
            "email" => user.email,
            "password" => valid_user_password()
          }
        })

      assert get_session(conn, :user_token)
      assert redirected_to(conn) =~ "/"

      # Now do a logged in request and assert on the menu
      conn = get(conn, "/")
      response = html_response(conn, 200)
      assert response =~ user.name
      assert response =~ "Settings</a>"
      assert response =~ "Sign out</a>"
    end

    @tag :unit
    test "logs the user in with remember me", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{
            "email" => user.email,
            "password" => valid_user_password(),
            "remember_me" => "true"
          }
        })

      assert conn.resp_cookies["_cxs_starter_web_user_remember_me"]
      assert redirected_to(conn) =~ "/"
    end

    @tag :unit
    test "logs the user in with return to", %{conn: conn, user: user} do
      conn =
        conn
        |> init_test_session(user_return_to: "/foo/bar")
        |> post(Routes.user_session_path(conn, :create), %{
          "user" => %{
            "email" => user.email,
            "password" => valid_user_password()
          }
        })

      assert redirected_to(conn) == "/foo/bar"
    end

    @tag :unit
    test "emits error message with invalid credentials", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{"email" => user.email, "password" => "invalid_password"}
        })

      response = html_response(conn, 200)
      assert response =~ "Sign in"
      assert response =~ "Invalid email or password"
    end
  end

  describe "DELETE /users/log_out" do
    @tag :unit
    test "logs the user out", %{conn: conn, user: user} do
      conn = conn |> sign_in_user(user) |> delete(Routes.user_session_path(conn, :delete))
      assert redirected_to(conn) == "/"
      refute get_session(conn, :user_token)
      assert get_flash(conn, :info) =~ "Logged out successfully"
    end

    @tag :unit
    test "succeeds even if the user is not logged in", %{conn: conn} do
      conn = delete(conn, Routes.user_session_path(conn, :delete))
      assert redirected_to(conn) == "/"
      refute get_session(conn, :user_token)
      assert get_flash(conn, :info) =~ "Logged out successfully"
    end
  end
end
